
PROGRAM GRAFCHARS; 

USES TURTLEGRAPHICS;

PROCEDURE BOXSTRING(X,Y: INTEGER; S: STRING);
(********************************)
(*                              *)
(* WRITE A STRING IN ERASED BOX *)
(*                              *)
(********************************)
VAR L,R,B,T: INTEGER;
BEGIN
  PENCOLOR(NONE); MOVETO(X,Y);
  L:=X-3; R:=X+2+7*LENGTH(S);
  B:=Y-2; T:=Y+10;
  VIEWPORT(L,R,B,T);
  FILLSCREEN(BLACK);
  VIEWPORT(0,279,0,191);
  PENCOLOR(NONE); MOVETO(L,B);
  PENCOLOR(WHITE); MOVETO(L,T);
  MOVETO(R,T); MOVETO(R,B); MOVETO(L,B);
  PENCOLOR(NONE); MOVETO(L+3,B+2);
  WSTRING(S);
END;


PROCEDURE WRITECHARS;
VAR HORIZ,VERT,ASCII: INTEGER;
BEGIN
  INITTURTLE;
  ASCII:=0;
  FOR VERT:=0 TO 18 DO
    BEGIN
      MOVETO(0,180-VERT*10);
      FOR HORIZ:=0 TO 39 DO
        BEGIN
          WCHAR(CHR(ASCII));
          ASCII:=(ASCII+1) MOD 128;
        END;
    END;
  READLN;
END;

  
  
PROCEDURE YOUTOO;
VAR I: INTEGER;
BEGIN
  INITTURTLE;
  PENCOLOR(WHITE);
  FOR I:=1 TO 300 DO
    BEGIN
      MOVE(I);
      TURN(-89);
    END;
  BOXSTRING(20,92,'YOU TOO CAN MIX TEXT AND GRAPHICS');
  READLN;
END;
  

PROCEDURE CIRCLE;
VAR I: INTEGER;
BEGIN
  INITTURTLE;
  MOVETO(119,104); WSTRING('POSITION');
  MOVETO(112,92); WSTRING('CHARACTERS');
  MOVETO(119,80);  WSTRING('ANYWHERE');
  CHARTYPE(6); (* XOR *)
  TURNTO(180);
  FOR I:=1 TO 300 DO
    BEGIN
      MOVETO(140,92);
      MOVE(85);
      TURN(-6);
      WCHAR(CHR(1));
    END;
  READLN;
END;
  
  
BEGIN; (* MAIN PROGRAM *)
  WRITELN('PRESS RETURN FOR MORE...');
  READLN;
  WRITECHARS;
  YOUTOO;
  CIRCLE;
  TEXTMODE;
  WRITELN('THAT''S ALL FOLKS...');
END.


