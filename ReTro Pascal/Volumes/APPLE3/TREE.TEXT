

PROGRAM TREE;

(**************************************)
(*                                    *)
(* MAKE AND PLOT A SIMPLE BINARY TREE *)
(* (PROGRAM BY BILL ATKINSON)         *)
(*                                    *)
(**************************************)

USES TURTLEGRAPHICS;

CONST WORDLENGTH=6;
      CONSOLE=1;

TYPE WORDSTRING=STRING[WORDLENGTH];
     NODEPTR=^NODE;
     NODE=RECORD
            WORD: WORDSTRING;
            LEFTPTR,RIGHTPTR: NODEPTR;
          END;
          
VAR ROOT: NODEPTR;
    S: STRING;
    W: WORDSTRING;
    DELTAY: ARRAY[0..4] OF INTEGER;
    

PROCEDURE INSERTIT(W: WORDSTRING; VAR P: NODEPTR);
BEGIN
  IF P=NIL THEN (* MAKE NEW NODE *) 
    BEGIN
      NEW(P);
      P^.LEFTPTR:=NIL;
      P^.RIGHTPTR:=NIL;
      P^.WORD:=W;
    END
  ELSE (* CHASE DOWN TREE FURTHER *)
    BEGIN
      IF W>P^.WORD THEN INSERTIT(W,P^.RIGHTPTR);
      IF W<P^.WORD THEN INSERTIT(W,P^.LEFTPTR);
    END;
END;


PROCEDURE PRINTTREE(P: NODEPTR);
BEGIN
  IF P<>NIL THEN 
    BEGIN
      PRINTTREE(P^.LEFTPTR);
      WRITELN(P^.WORD);
      PRINTTREE(P^.RIGHTPTR);
    END;
END;


PROCEDURE BOXWORD(W: WORDSTRING);
(* BEGIN AND END PEN AT LEFT OF BOX *)
VAR X,Y: INTEGER;
BEGIN
  X:=TURTLEX; Y:=TURTLEY;
  PENCOLOR(WHITE); MOVETO(X,Y+5); 
  MOVETO(X+44,Y+5); MOVETO(X+44,Y-5); 
  MOVETO(X,Y-5); MOVETO(X,Y); 
  PENCOLOR(NONE); MOVETO(X+2,Y-4); 
  WSTRING(W); MOVETO(X,Y);
END;



PROCEDURE SHOWTREE(P: NODEPTR; LEVEL: INTEGER);
VAR X,Y: INTEGER;
BEGIN
  IF P<>NIL THEN 
    BEGIN
      X:=TURTLEX;
      Y:=TURTLEY;
      BOXWORD(P^.WORD);
      IF LEVEL>4 THEN WRITE(CHR(7)); (* BEEP *)
      IF LEVEL<=4 THEN
        BEGIN
          PENCOLOR(NONE); MOVETO(X+44,Y+3);
          PENCOLOR(WHITE); MOVETO(X+56,Y+DELTAY[LEVEL]);
          SHOWTREE(P^.LEFTPTR,LEVEL+1);
          PENCOLOR(NONE); MOVETO(X+44,Y-3);
          PENCOLOR(WHITE); MOVETO(X+56,Y-DELTAY[LEVEL]);
          SHOWTREE(P^.RIGHTPTR,LEVEL+1);
        END;
    END;
END;


PROCEDURE GOODBYE;
BEGIN
  TEXTMODE;
  WRITELN; WRITELN('THAT''S ALL FOLKS...');
  EXIT(PROGRAM);
END;


BEGIN
  ROOT:=NIL;
  DELTAY[0]:=48;
  DELTAY[1]:=24;
  DELTAY[2]:=12;
  DELTAY[3]:=6;
  DELTAY[4]:=3;
  
  REPEAT
    WRITE('ENTER WORD: ');
    UNITCLEAR(CONSOLE); (* NO TYPE-AHEAD *)
    READLN(S);
    IF LENGTH(S)=0 THEN GOODBYE;
    IF LENGTH(S)<=WORDLENGTH THEN W:=S
      ELSE W:=COPY(S,1,WORDLENGTH);
    INSERTIT(W,ROOT);
    WRITELN; 
    WRITELN('THE WORDS IN ORDER ARE:');
    PRINTTREE(ROOT);
    WRITELN; READLN;
    INITTURTLE; MOVETO(0,170); 
    WSTRING('BINARY TREE:');
    MOVETO(0,96); 
    SHOWTREE(ROOT,0);
    READLN; TEXTMODE;
  UNTIL FALSE; (* TILL ZERO LENGTH EXIT *)
END.


