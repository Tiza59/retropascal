
PROGRAM HILBERT;

USES TURTLEGRAPHICS;

VAR SIZE,DELTA,N: INTEGER;
    ORDER: INTEGER;
    CH: CHAR;
    
PROCEDURE HIL(I:INTEGER);
VAR A,B: INTEGER;
  
  PROCEDURE HIL1;
  BEGIN
    TURN(A); HIL(-B); TURN(A);
  END; (* HIL1 *)
  
  PROCEDURE HIL2;
  BEGIN
    MOVE(SIZE);
    HIL(B);
    TURN(-A); MOVE(SIZE); TURN(-A);
    HIL(B);
    MOVE(SIZE);
  END; (* HIL2 *)
  
BEGIN (* HIL *)
  IF I=0 THEN TURN(180)
  ELSE 
  BEGIN
    IF I>0 THEN 
      BEGIN
        A:=90; B:=I-1;
      END
    ELSE BEGIN
      A:=-90; B:=I+1;
    END;
    HIL1; HIL2; HIL1;
  END;
END; (* HIL *)

BEGIN (* MAIN PROGRAM *)
  WRITELN('WELCOME TO HILBERT');
  WRITELN('ENTER ORDER 0 TO QUIT.');
  REPEAT (* FOR EACH ORDER *)
    WRITE('ORDER: ');  READLN(ORDER);
    IF (ORDER < 1) OR (ORDER > 7) THEN
      BEGIN
        WRITELN('THAT''S ALL FOLKS...');
        EXIT(PROGRAM);
      END;
    INITTURTLE;
    DELTA:=2;
    (* CALC STARTING X,Y AND SIZE *)
    FOR N:=2 TO ORDER DO DELTA:=DELTA*2;
    DELTA:=DELTA-1;
    SIZE:=190 DIV DELTA;
    DELTA:=(DELTA*SIZE) DIV 2;
    MOVETO(140-DELTA,96-DELTA);
    PENCOLOR(WHITE);
    HIL(ORDER);
    (* WAIT FOR KEYSTROKE,THEN SWITCH TO TEXT *)
    READ(KEYBOARD,CH); TEXTMODE;  
  UNTIL FALSE;
END.


